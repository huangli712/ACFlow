#
# Project : Gardenia
# Source  : som.jl
# Author  : Li Huang (huangli@caep.cn)
# Status  : Unstable
#
# Last modified: 2023/01/21
#

#=
### *Customized Structs* : *StochOM Solver*
=#

"""
    Box

Rectangle. The field configuration consists of many boxes. They exhibit
various areas (width √ó height). We use the Metropolis important sampling
algorithm to sample them and evaluate their contributions to the spectrum.

### Members

* h -> Height of the box.
* w -> Width of the box.
* c -> Position of the box.
"""
mutable struct Box
    h :: F64
    w :: F64
    c :: F64
end

"""
    StochOMElement

Mutable struct. It is used to record the field configurations, which will
be sampled by Monte Carlo sweeping procedure.

### Members

* C -> Field configuration.
* Œõ -> Contributions of the field configuration to the correlator.
* G -> Reproduced correlator.
* Œî -> Difference between reproduced and raw correlators.
"""
mutable struct StochOMElement
    C :: Vector{Box}
    Œõ :: Array{F64,2}
    G :: Vector{F64}
    Œî :: F64
end

"""
    StochOMContext

Mutable struct. It is used within the StochOM solver only.

### Members

* G·µ•    -> Input data for correlator.
* œÉ¬π    -> Actually 1.0 / œÉ¬π.
* grid  -> Grid for input data.
* mesh  -> Mesh for output spectrum.
* C·µ•    -> It is used to record the field configurations for all attempts.
* Œî·µ•    -> It is used to record the errors for all attempts.
"""
mutable struct StochOMContext
    G·µ•   :: Vector{F64}
    œÉ¬π   :: Vector{F64}
    grid :: AbstractGrid
    mesh :: AbstractMesh
    C·µ•   :: Vector{Vector{Box}}
    Œî·µ•   :: Vector{F64}
end

#=
### *Global Drivers*
=#

"""
    solve(S::StochOMSolver, rd::RawData)

Solve the analytical continuation problem by the stochastic optimization
method.
"""
function solve(S::StochOMSolver, rd::RawData)
    println("[ StochOM ]")
    MC, SC = init(S, rd)

    # Parallel version
    if nworkers() > 1
        println("Using $(nworkers()) workers")
        #
        # Copy configuration dicts
        p1 = deepcopy(PBASE)
        p2 = deepcopy(PStochOM)
        #
        #  Launch the tasks one by one
        ùêπ = Future[]
        for i = 1:nworkers()
            ùëì = @spawnat i + 1 prun(S, p1, p2, MC, SC)
            push!(ùêπ, ùëì)
        end
        #
        # Wait and collect the solutions
        sol = []
        for i = 1:nworkers()
            wait(ùêπ[i])
            push!(sol, fetch(ùêπ[i]))
        end
        #
        # Average the solutions
        Aout = similar(sol[end])
        fill!(Aout, 0.0)
        for i in eachindex(sol)
            @. Aout = Aout + sol[i] / nworkers()
        end
        #
        # Postprocess the solutions
        Gout = last(SC, Aout)

    # Sequential version
    else
        Aout = run(MC, SC)
        Gout = last(SC, Aout)

    end

    return SC.mesh.mesh, Aout, Gout
end

"""
    init(S::StochOMSolver, rd::RawData)

Initialize the StochOM solver and return the StochOMMC and StochOMContext
structs.
"""
function init(S::StochOMSolver, rd::RawData)
    MC = init_mc(S)
    println("Create infrastructure for Monte Carlo sampling")

    G·µ•, œÉ¬π = init_iodata(S, rd)
    println("Postprocess input data: ", length(œÉ¬π), " points")

    grid = make_grid(rd)
    println("Build grid for input data: ", length(grid), " points")

    mesh = make_mesh()
    println("Build mesh for spectrum: ", length(mesh), " points")

    C·µ•, Œî·µ• = init_context(S)
    SC = StochOMContext(G·µ•, œÉ¬π, grid, mesh, C·µ•, Œî·µ•)

    return MC, SC
end

"""
    run(MC::StochOMMC, SC::StochOMContext)

Perform stochastic optimization simulation, sequential version.
"""
function run(MC::StochOMMC, SC::StochOMContext)
    # Setup essential parameters
    ntry = get_s("ntry")
    nstep = get_s("nstep")

    # Sample and collect data
    for l = 1:ntry
        SE = init_element(MC, SC)

        for _ = 1:nstep
            update(MC, SE, SC)
        end

        SC.Œî·µ•[l] = SE.Œî
        SC.C·µ•[l] = deepcopy(SE.C)
        @printf("try -> %6i (%6i) Œî -> %8.4e \n", l, ntry, SE.Œî)
        flush(stdout)
        (l % 10 == 0) && write_statistics(MC)
    end

    return average(SC)
end

"""
    prun(S::StochOMSolver,
         p1::Dict{String,Vector{Any}},
         p2::Dict{String,Vector{Any}},
         MC::StochOMMC, SC::StochOMContext)

Perform stochastic optimization simulation, parallel version.
The arguments `p1` and `p2` are copies of PBASE and PStochOM, respectively.
"""
function prun(S::StochOMSolver,
              p1::Dict{String,Vector{Any}},
              p2::Dict{String,Vector{Any}},
              MC::StochOMMC, SC::StochOMContext)
    # Revise parameteric dicts
    rev_dict(p1)
    rev_dict(S, p2)

    # Initialize random number generator again
    MC.rng = MersenneTwister(rand(1:10000) * myid() + 1981)

    # Setup essential parameters
    ntry = get_s("ntry")
    nstep = get_s("nstep")

    # Sample and collect data
    for l = 1:ntry
        SE = init_element(MC, SC)

        for _ = 1:nstep
            update(MC, SE, SC)
        end

        SC.Œî·µ•[l] = SE.Œî
        SC.C·µ•[l] = deepcopy(SE.C)
        @printf("try -> %6i (%6i) Œî -> %8.4e \n", l, ntry, SE.Œî)
        flush(stdout)
        (myid() == 2) && (l % 10 == 0) && write_statistics(MC)
    end

    return average(SC)
end

"""
    average(SC::StochOMContext)

Postprocess the collected results after the stochastic optimization
simulations. It will generate the spectral functions.
"""
function average(SC::StochOMContext)
    nmesh = get_b("nmesh")
    ntry  = get_s("ntry")

    # Calculate the median of SC.Œî·µ•
    dev_ave = median(SC.Œî·µ•)

    # Determine the Œ±good parameter, which is used to filter the
    # calculated spectra.
    Œ±good = 1.2
    if count(x -> x < dev_ave / Œ±good, SC.Œî·µ•) == 0
        Œ±good = 1.0
    end

    # Accumulate the final spectrum
    Aom = zeros(F64, nmesh)
    for l = 1:ntry
        if SC.Œî·µ•[l] < dev_ave / Œ±good
            for w = 1:nmesh
                _omega = SC.mesh[w]
                for r = 1:length(SC.C·µ•[l])
                    R = SC.C·µ•[l][r]
                    if R.c - 0.5 * R.w ‚â§ _omega ‚â§ R.c + 0.5 * R.w
                        Aom[w] = Aom[w] + R.h
                    end
                end
            end
        end
    end

    # Normalize the spectrum
    Lgood = count(x -> x < dev_ave / Œ±good, SC.Œî·µ•)
    @. Aom = Aom / Lgood

    @printf("Median œá¬≤ : %16.12e Accepted configurations : %5i\n", dev_ave, Lgood)

    return Aom
end

"""
    last(SC::StochOMContext, Aout::Vector{F64})

It will process and write the calculated results by the StochOM solver,
including final spectral function and reproduced correlator.
"""
function last(SC::StochOMContext, Aout::Vector{F64})
    # Write the spectral function
    write_spectrum(SC.mesh, Aout)

    # Reproduce input data and write them
    kernel = make_kernel(SC.mesh, SC.grid)
    G = reprod(SC.mesh, kernel, Aout)
    write_backward(SC.grid, G)

    # Calculate full response function on real axis and write them
    if get_b("ktype") == "fermi"
        _G = kramers(SC.mesh, Aout)
    else
        _G = kramers(SC.mesh, Aout .* SC.mesh)
    end
    write_complete(SC.mesh, _G)

    return _G
end

#=
### *Core Algorithms*
=#

"""
    update(MC::StochOMMC, SE::StochOMElement, SC::StochOMContext)

Using the Metropolis algorithm to update the field configuration, i.e, a
collection of hundreds of boxes.
"""
function update(MC::StochOMMC, SE::StochOMElement, SC::StochOMContext)
    Tmax = 100 # Length of the Markov chain
    nbox = get_s("nbox")

    ST = deepcopy(SE)

    # The Markov chain is divided into two stages
    T1 = rand(MC.rng, 1:Tmax)
    d1 = rand(MC.rng, F64)
    d2 = 1.0 + rand(MC.rng, F64)
    #
    # The first stage
    for _ = 1:T1
        update_type = rand(MC.rng, 1:7)

        @cswitch update_type begin
            @case 1
                if length(ST.C) < nbox
                    try_insert(MC, ST, SC, d1)
                end
                break

            @case 2
                if length(ST.C) > 1
                    try_remove(MC, ST, SC, d1)
                end
                break

            @case 3
                try_shift(MC, ST, SC, d1)
                break

            @case 4
                try_width(MC, ST, SC, d1)
                break

            @case 5
                if length(ST.C) > 1
                    try_height(MC, ST, SC, d1)
                end
                break

            @case 6
                if length(ST.C) < nbox
                    try_split(MC, ST, SC, d1)
                end
                break

            @case 7
                if length(ST.C) > 1
                    try_merge(MC, ST, SC, d1)
                end
                break
        end

    end
    #
    # The second stage
    for _ = T1+1:Tmax
        update_type = rand(MC.rng, 1:7)

        @cswitch update_type begin
            @case 1
                if length(ST.C) < nbox
                    try_insert(MC, ST, SC, d2)
                end
                break

            @case 2
                if length(ST.C) > 1
                    try_remove(MC, ST, SC, d2)
                end
                break

            @case 3
                try_shift(MC, ST, SC, d2)
                break

            @case 4
                try_width(MC, ST, SC, d2)
                break

            @case 5
                if length(ST.C) > 1
                    try_height(MC, ST, SC, d2)
                end
                break

            @case 6
                if length(ST.C) < nbox
                    try_split(MC, ST, SC, d2)
                end
                break

            @case 7
                if length(ST.C) > 1
                    try_merge(MC, ST, SC, d2)
                end
                break
        end
    end

    if ST.Œî < SE.Œî
        SE.C = deepcopy(ST.C)
        SE.Œõ .= ST.Œõ
        SE.G .= ST.G
        SE.Œî  = ST.Œî
    end
end

#=
### *Service Functions*
=#

"""
    init_mc(S::StochOMSolver)

Try to create a StochOMMC struct.

See also: [`StochOMMC`](@ref).
"""
function init_mc(S::StochOMSolver)
    seed = rand(1:100000000)
    rng = MersenneTwister(seed)
    Macc = zeros(I64, 7)
    Mtry = zeros(I64, 7)

    MC = StochOMMC(rng, Macc, Mtry)

    return MC
end

"""
    init_element(MC::StochOMMC, SC::StochOMContext)

Try to initialize a StochOMElement struct.

See also: [`StochOMElement`](@ref).
"""
function init_element(MC::StochOMMC, SC::StochOMContext)
    wmin = get_b("wmin")
    wmax = get_b("wmax")
    nbox = get_s("nbox")
    sbox = get_s("sbox")
    wbox = get_s("wbox")

    # Generate weights randomly
    _Know = rand(MC.rng, 2:nbox)
    _weight = zeros(F64, _Know)
    for i = 1:_Know
        _weight[i] = rand(MC.rng, F64)
    end
    _weight[end] = 1.0

    # Sort weights, make sure the sum of weights is always 1.0.
    sort!(_weight)
    weight = diff(_weight)
    insert!(weight, 1, _weight[1])
    sort!(weight)

    # Make sure that each weight is larger than sbox.
    plus_count = 1
    minus_count = _Know
    while weight[plus_count] < sbox
        while weight[minus_count] < 2 * sbox
            minus_count = minus_count - 1
        end
        weight[plus_count] = weight[plus_count] + sbox
        weight[minus_count] = weight[minus_count] - sbox
        plus_count = plus_count + 1
    end

    # Create some boxes with random c, w, and h.
    C = Box[]
    Œõ = zeros(F64, length(SC.G·µ•), nbox)
    Œî = 0.0
    #
    for k = 1:_Know
        c = wmin + wbox / 2.0 + (wmax - wmin - wbox) * rand(MC.rng, F64)
        w = wbox + (min(2.0 * (c - wmin), 2.0 * (wmax - c)) - wbox) * rand(MC.rng, F64)
        while !constraints(c - w/2, c + w/2)
            c = wmin + wbox / 2.0 + (wmax - wmin - wbox) * rand(MC.rng, F64)
            w = wbox + (min(2.0 * (c - wmin), 2.0 * (wmax - c)) - wbox) * rand(MC.rng, F64)
        end
        h = weight[k] / w
        R = Box(h, w, c)
        push!(C, R)
        Œõ[:,k] .= calc_lambda(R, SC.grid)
    end
    #
    # Calculate green's function and relative error using boxes
    G = calc_green(Œõ, _Know)
    Œî = calc_error(G, SC.G·µ•, SC.œÉ¬π)

    return StochOMElement(C, Œõ, G, Œî)
end

"""
    init_iodata(S::StochOMSolver, rd::RawData)

Preprocess the input data (`rd`).

See also: [`RawData`](@ref), [`GreenData`](@ref).
"""
function init_iodata(S::StochOMSolver, rd::RawData)
    G = make_data(rd)
    G·µ• = G.value
    œÉ¬π = 1.0 ./ G.error

    return G·µ•, œÉ¬π
end

"""
    init_context(S::StochOMSolver)

Try to initialize the key members of a StochOMContext struct.

See also: [`StochOMContext`](@ref).
"""
function init_context(S::StochOMSolver)
    ntry = get_s("ntry")
    nbox = get_s("nbox")

    Œîv = zeros(F64, ntry)

    Cv = []
    for _ = 1:ntry
        C = Box[]
        for _ = 1:nbox
            push!(C, Box(0.0, 0.0, 0.0))
        end
        push!(Cv, C)
    end

    return Cv, Œîv
end

#=
*Remarks* :

Here, we would like to calculate the ``\Lambda`` function:

```math
\begin{equation}
\Lambda(\omega_n) = \int^{\infty}_{-\infty}
    d\omega~K(\omega_n,\omega) A(\omega).
\end{equation}
```

For a given box ``R``, its contribution to the ``\Lambda`` function is

```math
\begin{equation}
Œõ_{R}(\omega_n) = h \int^{c+w/2}_{c-w/2}
    d\omega~K(\omega_n,\omega),
\end{equation}
```

where ``h``, ``w``, and ``c`` denote the height, width, and center of the
box ``R``. Next, we will show you how to calculate ``\Lambda_R(\omega_n)``.
We can use `sympy` to derive the integral formula.

**A** For fermionic Matsubara frequency system.

```math
\begin{equation}
Œõ_{R}(\omega_n) = h \int^{c+w/2}_{c-w/2}
    d\omega~\frac{1}{i\omega_n - \omega}.
\end{equation}
```

The python codes are as follows:

```python
>>> from sympy import *
>>> w, x, a, b, h = symbols("w x a b h")
>>> integrate(h/(w - x), (x, a, b))
h*log(a - w) - h*log(b - w)
```

So, we have

```math
\begin{equation}
Œõ_{R}(\omega_n) = h \log
    \left(\frac{i\omega_n - c + w/2}{i\omega_n - c - w/2}\right).
\end{equation}
```

**B** For bosonic Matsubara frequency system.

```math
\begin{equation}
Œõ_{R}(\omega_n) = h \int^{c+w/2}_{c-w/2}
    d\omega~\frac{\omega}{i\omega_n - \omega}.
\end{equation}
```

The python codes are as follows:

```python
>>> from sympy import *
>>> w, x, a, b, h = symbols("w x a b h")
>>> f = integrate(h*x/(w - x), (x, a, b))
>>> simplify(f)
h*(a - b + w*log(a - w) - w*log(b - w))
```

So, we have

```math
\begin{equation}
Œõ_{R}(\omega_n) = h * \left[-w + i\omega_n * \log
    \left(\frac{i\omega_n - c + w/2}{i\omega_n - c - w/2}\right)
\right].
\end{equation}
```

**C** For bosonic Matsubara frequency system (symmetric version).

```math
\begin{equation}
Œõ_{R}(\omega_n) = h \int^{c+w/2}_{c-w/2}
    d\omega~\frac{\omega^2}{\omega_n^2 + \omega^2}.
\end{equation}
```

We can utilize the following formula:

```math
\begin{equation}
\int \frac{x^2}{a^2 + x^2} dx =
    x - a~\tan^{-1}\left(\frac{x}{a}\right)
\end{equation}
```

Thus,

```math
\begin{equation}
Œõ_{R}(\omega_n) = h
    \left[
        \omega - \omega_n \tan^{-1} \left(\frac{\omega}{\omega_n}\right)
    \right]
    \bigg|^{c+w/2}_{c-w/2}
\end{equation}
```

Finally, we have

```math
\begin{equation}
Œõ_{R}(\omega_n) = h
    \left[
        w +
        \omega_n \tan^{-1} \left(\frac{c - w/2}{\omega_n}\right)
        -
        \omega_n \tan^{-1} \left(\frac{c + w/2}{\omega_n}\right)
    \right]
\end{equation}
```

We have implemented the above formulas in `calc_lambda()`.
=#

"""
    calc_lambda(r::Box, grid::FermionicMatsubaraGrid)

Try to calculate the contribution of a given box `r` to the Œõ function.
This function works for FermionicMatsubaraGrid only.

See also: [`FermionicMatsubaraGrid`](@ref).
"""
function calc_lambda(r::Box, grid::FermionicMatsubaraGrid)
    e‚ÇÅ = r.c - 0.5 * r.w
    e‚ÇÇ = r.c + 0.5 * r.w
    iw = im * grid.œâ
    Œõ = @. r.h * log((iw - e‚ÇÅ) / (iw - e‚ÇÇ))
    return vcat(real(Œõ), imag(Œõ))
end

"""
    calc_lambda(r::Box, grid::FermionicImaginaryTimeGrid)

Try to calculate the contribution of a given box `r` to the Œõ function.
This function works for FermionicImaginaryTimeGrid only.

See also: [`FermionicImaginaryTimeGrid`](@ref).
"""
function calc_lambda(r::Box, grid::FermionicImaginaryTimeGrid)
    sorry()
end

"""
    calc_lambda(r::Box, grid::BosonicMatsubaraGrid)

Try to calculate the contribution of a given box `r` to the Œõ function.
This function works for BosonicMatsubaraGrid only.

See also: [`BosonicMatsubaraGrid`](@ref).
"""
function calc_lambda(r::Box, grid::BosonicMatsubaraGrid)
    ktype = get_b("ktype")

    e‚ÇÅ = r.c - 0.5 * r.w
    e‚ÇÇ = r.c + 0.5 * r.w

    if ktype == "bsymm"
        Œõ = @. atan( e‚ÇÅ / grid.œâ ) - atan( e‚ÇÇ / grid.œâ )
        Œõ = r.h * (r.w .+ grid.œâ .* Œõ)
        return Œõ
    else
        iw = im * grid.œâ
        Œõ = @. r.h * (-r.w + iw * log((iw - e‚ÇÅ) / (iw - e‚ÇÇ)))
        return vcat(real(Œõ), imag(Œõ))
    end
end

"""
    calc_lambda(r::Box, grid::BosonicImaginaryTimeGrid)

Try to calculate the contribution of a given box `r` to the Œõ function.
This function works for BosonicImaginaryTimeGrid only.

See also: [`BosonicImaginaryTimeGrid`](@ref).
"""
function calc_lambda(r::Box, grid::BosonicImaginaryTimeGrid)
    ktype = get_b("ktype")

    if ktype == "bsymm"
        Œõ = @. r.h * exp(-1 * grid.œÑ * r.c) * sinh(0.5 * grid.œÑ * r.w)
        Œõ = Œõ .* œÄ ./ grid.œÑ
        return Œõ
    else
        sorry()
    end
end

"""
    calc_error(G::Vector{F64}, G·µ•::Vector{F64}, œÉ¬π::Vector{F64})

Try to calculate œá¬≤. Here `G·µ•` and `œÉ¬π` denote the raw correlator and
related standard deviation. `G` means the reproduced correlator.

See also: [`calc_green`](@ref).
"""
function calc_error(G::Vector{F64}, G·µ•::Vector{F64}, œÉ¬π::Vector{F64})
    return sum( ( (G .- G·µ•) .* œÉ¬π ) .^ 2.0 )
end

"""
    calc_green(Œõ::Array{F64,2}, nk::I64)

Try to reconstruct the correlator via the field configuration.

See also: [`calc_error`](@ref).
"""
function calc_green(Œõ::Array{F64,2}, nk::I64)
    ngrid, nbox = size(Œõ)
    @assert nk ‚â§ nbox

    G = zeros(F64, ngrid)
    for k = 1:nk
        for g = 1:ngrid
            G[g] = G[g] + Œõ[g,k]
        end
    end

    return G
end

"""
    calc_norm(C::Vector{Box})

Calculate the total area of all boxes.
"""
function calc_norm(C::Vector{Box})
    norm = sum(map(x -> x.h * x.w, C))
    return norm
end

"""
    constraints(e‚ÇÅ::F64, e‚ÇÇ::F64)

This function is used to judege whether a given box overlapes with the
forbidden zone. Here `e‚ÇÅ` and `e‚ÇÇ` denote the left and right boundaries
of the box.
"""
function constraints(e‚ÇÅ::F64, e‚ÇÇ::F64)
    exclude = get_b("exclude")
    @assert e‚ÇÅ ‚â§ e‚ÇÇ

    if !isa(exclude, Missing)
        for i in eachindex(exclude)
            if e‚ÇÅ ‚â§ exclude[i][1] ‚â§ e‚ÇÇ ‚â§ exclude[i][2]
                return false
            end
            if exclude[i][1] ‚â§ e‚ÇÅ ‚â§ exclude[i][2] ‚â§ e‚ÇÇ
                return false
            end
            if exclude[i][1] ‚â§ e‚ÇÅ ‚â§ e‚ÇÇ ‚â§ exclude[i][2]
                return false
            end
            if e‚ÇÅ ‚â§ exclude[i][1] ‚â§ exclude[i][2] ‚â§ e‚ÇÇ
                return false
            end
        end
    end

    return true
end

"""
    try_insert(MC::StochOMMC,
               SE::StochOMElement,
               SC::StochOMContext,
               dacc::F64)

Insert a new box into the field configuration.
"""
function try_insert(MC::StochOMMC,
                    SE::StochOMElement,
                    SC::StochOMContext,
                    dacc::F64)
    sbox = get_s("sbox")
    wbox = get_s("wbox")
    wmin = get_b("wmin")
    wmax = get_b("wmax")
    csize = length(SE.C)

    # Choose a box randomly
    t = rand(MC.rng, 1:csize)

    # Check area of this box
    R = SE.C[t]
    if R.h * R.w ‚â§ 2.0 * sbox
        return
    end

    # Determine minimum and maximum areas of the new box
    dx_min = sbox
    dx_max = R.h * R.w - sbox
    if dx_max ‚â§ dx_min
        return
    end

    # Determine parameters for the new box
    r1 = rand(MC.rng, F64)
    r2 = rand(MC.rng, F64)
    c = (wmin + wbox / 2.0) + (wmax - wmin - wbox) * r1
    w_new_max = 2.0 * min(wmax - c, c - wmin)
    dx = Pdx(dx_min, dx_max, MC.rng)
    h = dx / w_new_max + (dx / wbox - dx / w_new_max) * r2
    w = dx / h

    # Rnew will be used to update Box t, while Radd is the new box.
    if !constraints(c - w/2, c + w/2)
        return
    end
    Rnew = Box(R.h - dx / R.w, R.w, R.c)
    Radd = Box(h, w, c)

    # Calculate update for Œõ
    G1 = SE.Œõ[:,t]
    G2 = calc_lambda(Rnew, SC.grid)
    G3 = calc_lambda(Radd, SC.grid)

    # Calculate new Œî function, it is actually the error function.
    Œî = calc_error(SE.G - G1 + G2 + G3, SC.G·µ•, SC.œÉ¬π)

    # Apply the Metropolis algorithm
    if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
        # Update box t
        SE.C[t] = Rnew

        # Add new Box
        push!(SE.C, Radd)

        # Update Œî, G, and Œõ.
        SE.Œî = Œî
        @. SE.G = SE.G - G1 + G2 + G3
        @. SE.Œõ[:,t] = G2
        @. SE.Œõ[:,csize+1] = G3

        # Update the counter
        MC.Macc[1] = MC.Macc[1] + 1
    end

    # Update the counter
    MC.Mtry[1] = MC.Mtry[1] + 1
end

"""
    try_remove(MC::StochOMMC,
               SE::StochOMElement,
               SC::StochOMContext,
               dacc::F64)

Remove an old box from the field configuration.
"""
function try_remove(MC::StochOMMC,
                    SE::StochOMElement,
                    SC::StochOMContext,
                    dacc::F64)
    csize = length(SE.C)

    # Choose two boxes randomly
    # Box t1 will be removed, while box t2 will be modified.
    t1 = rand(MC.rng, 1:csize)
    t2 = rand(MC.rng, 1:csize)
    #
    while t1 == t2
        t2 = rand(MC.rng, 1:csize)
    end
    #
    if t1 < t2
        t1, t2 = t2, t1
    end

    # Get box t1 and box t2
    R1 = SE.C[t1]
    R2 = SE.C[t2]
    Re = SE.C[end]

    # Generate new box t2
    dx = R1.h * R1.w
    R2n = Box(R2.h + dx / R2.w, R2.w, R2.c)

    # Calculate update for Œõ
    G1 = SE.Œõ[:,t1]
    G2 = SE.Œõ[:,t2]
    Ge = SE.Œõ[:,csize]
    G2n = calc_lambda(R2n, SC.grid)

    # Calculate new Œî function, it is actually the error function.
    Œî = calc_error(SE.G - G1 - G2 + G2n, SC.G·µ•, SC.œÉ¬π)

    # Apply the Metropolis algorithm
    if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
        # Update box t2
        SE.C[t2] = R2n

        # Backup the last box in box t1
        if t1 < csize
            SE.C[t1] = Re
        end

        # Delete the last box, since its value has been stored in t1.
        pop!(SE.C)

        # Update Œî, G, and Œõ.
        SE.Œî = Œî
        @. SE.G = SE.G - G1 - G2 + G2n
        @. SE.Œõ[:,t2] = G2n
        if t1 < csize
            @. SE.Œõ[:,t1] = Ge
        end

        # Update the counter
        MC.Macc[2] = MC.Macc[2] + 1
    end

    # Update the counter
    MC.Mtry[2] = MC.Mtry[2] + 1
end

"""
    try_shift(MC::StochOMMC,
              SE::StochOMElement,
              SC::StochOMContext,
              dacc::F64)

Change the position of given box in the field configuration.
"""
function try_shift(MC::StochOMMC,
                   SE::StochOMElement,
                   SC::StochOMContext,
                   dacc::F64)
    wmin = get_b("wmin")
    wmax = get_b("wmax")
    csize = length(SE.C)

    # Choose a box randomly
    t = rand(MC.rng, 1:csize)

    # Retreive the box t
    R = SE.C[t]

    # Determine left and right boundaries for the center of the box
    dx_min = wmin + R.w / 2.0 - R.c
    dx_max = wmax - R.w / 2.0 - R.c
    if dx_max ‚â§ dx_min
        return
    end

    # Calculate Œ¥c and generate shifted box
    dc = Pdx(dx_min, dx_max, MC.rng)
    if !constraints(R.c + dc - R.w/2, R.c + dc + R.w/2)
        return
    end
    Rn = Box(R.h, R.w, R.c + dc)

    # Calculate update for Œõ
    G1 = SE.Œõ[:,t]
    G2 = calc_lambda(Rn, SC.grid)

    # Calculate new Œî function, it is actually the error function.
    Œî = calc_error(SE.G - G1 + G2, SC.G·µ•, SC.œÉ¬π)

    # Apply the Metropolis algorithm
    if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
        # Update box t
        SE.C[t] = Rn

        # Update Œî, G, and Œõ.
        SE.Œî = Œî
        @. SE.G = SE.G - G1 + G2
        @. SE.Œõ[:,t] = G2

        # Update the counter
        MC.Macc[3] = MC.Macc[3] + 1
    end

    # Update the counter
    MC.Mtry[3] = MC.Mtry[3] + 1
end

"""
    try_width(MC::StochOMMC,
              SE::StochOMElement,
              SC::StochOMContext,
              dacc::F64)

Change the width and height of given box in the field configuration. Note
that the box's area is kept.
"""
function try_width(MC::StochOMMC,
                   SE::StochOMElement,
                   SC::StochOMContext,
                   dacc::F64)
    wbox = get_s("wbox")
    wmin = get_b("wmin")
    wmax = get_b("wmax")
    csize = length(SE.C)

    # Choose a box randomly
    t = rand(MC.rng, 1:csize)

    # Retreive the box t
    R = SE.C[t]

    # Determine left and right boundaries for the width of the box
    weight = R.h * R.w
    dx_min = wbox - R.w
    dx_max = min(2.0 * (R.c - wmin), 2.0 * (wmax - R.c)) - R.w
    if dx_max ‚â§ dx_min
        return
    end

    # Calculate Œ¥w and generate new box
    dw = Pdx(dx_min, dx_max, MC.rng)
    w = R.w + dw
    h = weight / w
    c = R.c
    if !constraints(c - w/2, c + w/2)
        return
    end
    Rn = Box(h, w, c)

    # Calculate update for Œõ
    G1 = SE.Œõ[:,t]
    G2 = calc_lambda(Rn, SC.grid)

    # Calculate new Œî function, it is actually the error function.
    Œî = calc_error(SE.G - G1 + G2, SC.G·µ•, SC.œÉ¬π)

    # Apply the Metropolis algorithm
    if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
        # Update box t
        SE.C[t] = Rn

        # Update Œî, G, and Œõ.
        SE.Œî = Œî
        @. SE.G = SE.G - G1 + G2
        @. SE.Œõ[:,t] = G2

        # Update the counter
        MC.Macc[4] = MC.Macc[4] + 1
    end

    # Update the counter
    MC.Mtry[4] = MC.Mtry[4] + 1
end

"""
    try_height(MC::StochOMMC,
               SE::StochOMElement,
               SC::StochOMContext,
               dacc::F64)

Change the heights of two given boxes in the field configuration.
"""
function try_height(MC::StochOMMC,
                    SE::StochOMElement,
                    SC::StochOMContext,
                    dacc::F64)
    sbox  = get_s("sbox")
    csize = length(SE.C)

    # Choose two boxes randomly
    t1 = rand(MC.rng, 1:csize)
    t2 = rand(MC.rng, 1:csize)
    while t1 == t2
        t2 = rand(MC.rng, 1:csize)
    end

    # Get box t1 and box t2
    R1 = SE.C[t1]
    R2 = SE.C[t2]

    # Determine left and right boundaries for the height of the box t1
    w1 = R1.w
    w2 = R2.w
    h1 = R1.h
    h2 = R2.h
    dx_min = sbox / w1 - h1
    dx_max = (h2 - sbox / w2) * w2 / w1
    if dx_max ‚â§ dx_min
        return
    end

    # Calculate Œ¥h and generate new box t1 and box t2
    dh = Pdx(dx_min, dx_max, MC.rng)
    R1n = Box(R1.h + dh, R1.w, R1.c)
    R2n = Box(R2.h - dh * w1 / w2, R2.w, R2.c)

    # Calculate update for Œõ
    G1A = SE.Œõ[:,t1]
    G1B = calc_lambda(R1n, SC.grid)
    G2A = SE.Œõ[:,t2]
    G2B = calc_lambda(R2n, SC.grid)

    # Calculate new Œî function, it is actually the error function.
    Œî = calc_error(SE.G - G1A + G1B - G2A + G2B, SC.G·µ•, SC.œÉ¬π)

    # Apply the Metropolis algorithm
    if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
        # Update box t1 and box t2
        SE.C[t1] = R1n
        SE.C[t2] = R2n

        # Update Œî, G, and Œõ.
        SE.Œî = Œî
        @. SE.G = SE.G - G1A + G1B - G2A + G2B
        @. SE.Œõ[:,t1] = G1B
        @. SE.Œõ[:,t2] = G2B

        # Update the counter
        MC.Macc[5] = MC.Macc[5] + 1
    end

    # Update the counter
    MC.Mtry[5] = MC.Mtry[5] + 1
end

"""
    try_split(MC::StochOMMC,
              SE::StochOMElement,
              SC::StochOMContext,
              dacc::F64)

Split a given box into two boxes in the field configuration.
"""
function try_split(MC::StochOMMC,
                   SE::StochOMElement,
                   SC::StochOMContext,
                   dacc::F64)
    wbox = get_s("wbox")
    sbox = get_s("sbox")
    wmin = get_b("wmin")
    wmax = get_b("wmax")
    csize = length(SE.C)

    # Choose a box randomly
    t = rand(MC.rng, 1:csize)

    # Retreive the box t
    R1 = SE.C[t]
    if R1.w ‚â§ 2 * wbox || R1.w * R1.h ‚â§ 2.0 * sbox
        return
    end

    # Determine height for new boxes (h and h)
    h = R1.h

    # Determine width for new boxes (w1 and w2)
    w1 = wbox + (R1.w - 2.0 * wbox) * rand(MC.rng, F64)
    w2 = R1.w - w1
    if w1 > w2
        w1, w2 = w2, w1
    end

    # Determine center for new boxes (c1 + dc1 and c2 + dc2)
    c1 = R1.c - R1.w / 2.0 + w1 / 2.0
    c2 = R1.c + R1.w / 2.0 - w2 / 2.0
    dx_min = wmin + w1 / 2.0 - c1
    dx_max = wmax - w1 / 2.0 - c1
    if dx_max ‚â§ dx_min
        return
    end
    dc1 = Pdx(dx_min, dx_max, MC.rng)
    dc2 = -1.0 * w1 * dc1 / w2
    if !constraints(c1 + dc1 - w1/2, c1 + dc1 + w1/2) ||
       !constraints(c2 + dc2 - w2/2, c2 + dc2 + w2/2)
        return
    end

    if (c1 + dc1 ‚â• wmin + w1 / 2.0) &&
       (c1 + dc1 ‚â§ wmax - w1 / 2.0) &&
       (c2 + dc2 ‚â• wmin + w2 / 2.0) &&
       (c2 + dc2 ‚â§ wmax - w2 / 2.0)

        # Generate two new boxes
        R2 = Box(h, w1, c1 + dc1)
        R3 = Box(h, w2, c2 + dc2)

        # Calculate update for Œõ
        G1 = SE.Œõ[:,t]
        Ge = SE.Œõ[:,csize]
        G2 = calc_lambda(R2, SC.grid)
        G3 = calc_lambda(R3, SC.grid)

        # Calculate new Œî function, it is actually the error function.
        Œî = calc_error(SE.G - G1 + G2 + G3, SC.G·µ•, SC.œÉ¬π)

        # Apply the Metropolis algorithm
        if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
            # Remove old box t and insert two new boxes
            SE.C[t] = SE.C[end]
            pop!(SE.C)
            push!(SE.C, R2)
            push!(SE.C, R3)

            # Update Œî, G, and Œõ.
            SE.Œî = Œî
            @. SE.G = SE.G - G1 + G2 + G3
            if t < csize
                @. SE.Œõ[:,t] = Ge
            end
            @. SE.Œõ[:,csize] = G2
            @. SE.Œõ[:,csize+1] = G3

            # Update the counter
            MC.Macc[6] = MC.Macc[6] + 1
        end
    end

    # Update the counter
    MC.Mtry[6] = MC.Mtry[6] + 1
end

"""
    try_merge(MC::StochOMMC,
              SE::StochOMElement,
              SC::StochOMContext,
              dacc::F64)

Merge two given boxes into one box in the field configuration.
"""
function try_merge(MC::StochOMMC,
                   SE::StochOMElement,
                   SC::StochOMContext,
                   dacc::F64)
    wmin = get_b("wmin")
    wmax = get_b("wmax")
    csize = length(SE.C)

    # Choose two boxes randomly
    t1 = rand(MC.rng, 1:csize)
    t2 = rand(MC.rng, 1:csize)
    while t1 == t2
        t2 = rand(MC.rng, 1:csize)
    end
    if t1 > t2
        t1, t2 = t2, t1
    end

    # Get box t1 and box t2
    R1 = SE.C[t1]
    R2 = SE.C[t2]

    # Determine h, w, and c for new box
    weight = R1.h * R1.w + R2.h * R2.w
    w_new = 0.5 * (R1.w + R2.w)
    h_new = weight / w_new
    c_new = R1.c + (R2.c - R1.c) * R2.h * R2.w / weight

    # Determine left and right boundaries for the center of the new box
    dx_min = wmin + w_new / 2.0 - c_new
    dx_max = wmax - w_new / 2.0 - c_new
    if dx_max ‚â§ dx_min
        return
    end

    # Calculate Œ¥c and generate new box
    dc = Pdx(dx_min, dx_max, MC.rng)
    if !constraints(c_new + dc - w_new/2, c_new + dc + w_new/2)
        return
    end
    Rn = Box(h_new, w_new, c_new + dc)

    # Calculate update for Œõ
    G1 = SE.Œõ[:,t1]
    G2 = SE.Œõ[:,t2]
    Ge = SE.Œõ[:,csize]
    Gn = calc_lambda(Rn, SC.grid)

    # Calculate new Œî function, it is actually the error function.
    Œî = calc_error(SE.G - G1 - G2 + Gn, SC.G·µ•, SC.œÉ¬π)

    # Apply the Metropolis algorithm
    if rand(MC.rng, F64) < ((SE.Œî/Œî) ^ (1.0 + dacc))
        # Update box t1 with new box
        SE.C[t1] = Rn

        # Delete box t2
        if t2 < csize
            SE.C[t2] = SE.C[end]
        end
        pop!(SE.C)

        # Update Œî, G, and Œõ.
        SE.Œî = Œî
        @. SE.G = SE.G - G1 - G2 + Gn
        @. SE.Œõ[:,t1] = Gn
        if t2 < csize
            @. SE.Œõ[:,t2] = Ge
        end

        # Update the counter
        MC.Macc[7] = MC.Macc[7] + 1
    end

    # Update the counter
    MC.Mtry[7] = MC.Mtry[7] + 1
end

#=
*Remarks* : *Probability Density Function*

Every Every proposed elementary update is parametrized by a real number
``\delta\xi \in [\delta\xi_{min}:\delta\xi_{max}]``. A concrete meaning
of ``\delta\xi`` depends on the elementary update in question. For
instance, ``\delta\xi`` can be a shift of the centre of an existing
rectangle, or the weight of a rectangle to be added. In general, ``\delta
\xi`` are defined so that larger ``|\delta\xi|`` correspond to more
prominent changes in the configuration. `StochOM` randomly generates
values of ``\delta\xi`` according to the following probability density
function:

```math
\begin{equation}
\mathcal{P}(\delta\xi) = N
\exp\left(-\gamma \frac{|\delta\xi|}{X}\right),
\end{equation}
```

```math
\begin{equation}
X \equiv \max(|\delta\xi_{min}|, |\delta\xi_{max}|),
\end{equation}
```

```math
\begin{equation}
N = \frac{\gamma}{X}
\left[
\text{sign}(\delta\xi_{min})\left(e^{-\gamma|\delta\xi_{min}|/X}-1\right) +
\text{sign}(\delta\xi_{max})\left(1-e^{-\gamma|\delta\xi_{max}|/X}\right)
\right]^{-1}
\end{equation}
```

User can change parameter ``\gamma`` to control non-uniformity of the PDF.
=#

"""
    Pdx(xmin::F64, xmax::F64, rng::AbstractRNG)

Try to calculate the probability density function.
"""
function Pdx(xmin::F64, xmax::F64, rng::AbstractRNG)
    xmin_abs = abs(xmin)
    xmax_abs = abs(xmax)
    X = max(xmin_abs, xmax_abs)

    Œ≥ = 2.0
    Œ≥_X = Œ≥ / X

    Œ∑ = rand(rng, F64)
    ùëÅ  = (1 - Œ∑) * copysign(expm1(-Œ≥_X * xmin_abs), xmin)
    ùëÅ +=      Œ∑  * copysign(expm1(-Œ≥_X * xmax_abs), xmax)

    return copysign( log1p(-abs(ùëÅ)) / Œ≥_X, ùëÅ)
end
